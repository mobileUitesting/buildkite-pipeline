
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id  'jacoco'
    id 'org.jetbrains.dokka'
}

android {
    signingConfigs {}

    compileSdkVersion 34
    useLibrary 'org.apache.http.legacy'
    ndkVersion '21.3.6528147'
    namespace "com.saucelabs.mydemoapp.android"
    defaultConfig {
        applicationId "com.saucelabs.mydemoapp.android"
        minSdkVersion 28
        targetSdkVersion 34
        versionCode 1 // CI automatically replaces this
        versionName "1.0.0" // CI automatically replaces this
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "SAUCE_USERNAME", "\"${System.getenv('SAUCE_USERNAME')}\""
        buildConfigField "String", "SAUCE_ACCESS_KEY", "\"${System.getenv('SAUCE_ACCESS_KEY')}\""
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources true
        }
    }

    buildTypes {
        debug {
            debuggable true
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    buildFeatures {
        dataBinding = true
        mlModelBinding true
        buildConfig = true
    }

    kotlinOptions {
        jvmTarget = '17'
    }
    lint {
        checkReleaseBuilds false
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
}

tasks.dokkaHtml {
    outputDirectory.set(layout.buildDirectory.dir("dokka/html"))
    dokkaSourceSets {
        configureEach {
            includeNonPublic.set(false)
            reportUndocumented.set(true)
            skipEmptyPackages.set(true)
            jdkVersion.set(17)
            noStdlibLink.set(true)
            noJdkLink.set(true)
            sourceRoots.from(file("src\\androidTest\\kotlin"))
        }
    }
}
tasks.dokkaJavadoc {
    outputDirectory.set(layout.buildDirectory.dir("dokka/javadoc"))
}




task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from(files([mainSrc]))
    classDirectories.from(files([debugTree]))

    executionData.from(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ]))
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'com.google.firebase:firebase-firestore-ktx:25.1.1'
    implementation 'com.google.firebase:firebase-auth-ktx:23.1.0'

    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0")
    implementation 'androidx.test.espresso:espresso-web:3.6.1'
    implementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    //Libraries For Testing
    androidTestImplementation 'com.saucelabs.visual:visual-espresso:0.0.1-RC'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:3.0.2")
    androidTestImplementation 'androidx.test:runner:1.6.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.material:material:1.12.0'

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    //Retrofit
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:33.0.0-android'

    // Utils
    implementation 'com.github.gcacace:signature-pad:1.3.1'
    implementation(name:'com.kyanogen.signatureview.signature-view', ext:'aar')
    implementation(name:'com.williamww.silky-signature', ext:'aar')
    implementation(name:'com.uphyca.creditcardedittext', ext:'aar')

    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation "com.squareup.okhttp3:okhttp:4.9.0"

    // Backtrace SDK
    implementation 'com.github.backtrace-labs.backtrace-android:backtrace-library:3.7.0'

    def camerax_version = "1.1.0"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "com.google.zxing:core:3.3.3"
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.0.21"

    // Timber for logging (optional but useful)
    implementation 'com.jakewharton.timber:timber:5.0.1'
    // Kotlin logging library (optional)
    implementation 'io.github.microutils:kotlin-logging:2.1.23'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.dokka'
